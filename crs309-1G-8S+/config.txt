# Reset configuration
/system reset-configuration no-defaults=yes skip-backup=yes

# Set up NTP
/system clock set time-zone-name=America/New_York
/system ntp client set enabled=yes 
/system ntp client servers add address=time.cloudflare.com
/system ntp client servers add address=time.nist.gov

# Set up bridge
/interface bridge add name=bridge1 frame-types=admit-only-vlan-tagged vlan-filtering=yes
/interface bridge port add bridge=bridge1 interface=sfp-sfpplus8 frame-types=admit-only-vlan-tagged

# Set up CLUSTER VLAN
/interface bridge vlan add bridge=bridge1 tagged=sfp-sfpplus8 vlan-ids=20
/interface bridge port
add bridge=bridge1 interface=sfp-sfpplus1 pvid=20 frame-types=admit-only-untagged-and-priority-tagged
add bridge=bridge1 interface=sfp-sfpplus2 pvid=20 frame-types=admit-only-untagged-and-priority-tagged
add bridge=bridge1 interface=sfp-sfpplus3 pvid=20 frame-types=admit-only-untagged-and-priority-tagged

# Set up MGMT VLAN
/interface vlan add name=MGMT interface=bridge1 vlan-id=99
/interface bridge vlan add bridge=bridge1 vlan-ids=99 tagged=bridge1,sfp-sfpplus8
/interface bridge port add bridge=bridge1 interface=ether1 pvid=99 frame-types=admit-only-untagged-and-priority-tagged
/ip address add address=10.0.0.2/24 interface=MGMT

# Set default route for inter-VLAN access
/ip route add gateway=10.0.0.1

# Set up NETMGMT
/interface list add name=NETMGMT
/interface list member add list=NETMGMT interface=MGMT

# Restrict these to NETMGMT
/tool mac-server set allowed-interface-list=NETMGMT
/tool mac-server mac-winbox set allowed-interface-list=NETMGMT
/ip neighbor discovery-settings set discover-interface-list=NETMGMT

# Disable these unneeded admin services
/ip service disable telnet,ftp

# Disable debug tools
/tool bandwidth-server set enabled=no

# Enable strong crypto for ssh
/ip ssh set strong-crypto=yes

# Generate certificates
/certificate
add name=AxolotlCloudNetworkCRS309CA common-name="Axolotl Cloud Network CA" country=US organization="Axolotl Cloud" trusted=yes key-usage=key-cert-sign,crl-sign days-valid=3650
sign AxolotlCloudNetworkCRS309CA
add name=WwwSsl common-name="WWW SSL" subject-alt-name=IP:10.0.0.2 country=US organization="Axolotl Cloud" trusted=yes key-usage=tls-server
sign ca=AxolotlCloudNetworkCRS309CA WwwSsl

/ip service set www-ssl certificate=WwwSsl

/ip service enable www-ssl
